&НаСервере
//*******************************************************************************
Функция ПолучитьМакет()
	
	Возврат РеквизитФормыВЗначение("Отчет").ПолучитьМакет("Макет");
	
КонецФункции
//*******************************************************************************
&НаСервере
Функция ПолучитьВидДокумента(Вид)
	
	Соответствие = Новый Соответствие;
	
	Соответствие.Вставить("СводныйВозврат",					 "ВозвратТоваровОтПокупателя");
	Соответствие.Вставить("СписаниеТоваров",				 "СписаниеТоваров");
	Соответствие.Вставить("СводнаяРеализация",				 "РеализацияТоваровУслуг");
	Соответствие.Вставить("ПриходныйКассовыйОрдер",			 "ПриходныйКассовыйОрдер");
	Соответствие.Вставить("РасходныйКассовыйОрдер",			 "РасходныйКассовыйОрдер");
	Соответствие.Вставить("РеализацияТоваровУслуг",			 "РеализацияТоваровУслуг");
	Соответствие.Вставить("ВозвратТоваровОтПокупателя",		 "ВозвратТоваровОтПокупателя");
	Соответствие.Вставить("СводноеПоступлениеТоваровУслуг",	 "ПоступлениеТоваровУслуг");
	
	Возврат Соответствие.Получить(Вид);
	
КонецФункции
//*******************************************************************************
&НаСервере
Функция ЗаменитьТекстЗапроса(ТекстЗапроса, ВидДокумента)
	
	Если ВидДокумента = "СписаниеТоваров" Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТЗ.Цена КАК Цена,",				 """-//-//-//-"" КАК Цена,"); 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТЗ.Сумма КАК Сумма,",				 """-//-//-//-"" КАК Сумма,"); 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Документ.Цена КАК Цена,",			 """-//-//-//-"" КАК Цена,"); 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Документ.Сумма КАК Сумма,",		 """-//-//-//-"" КАК Сумма,");	
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции
//*******************************************************************************
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ТабличныйДокумент.ОтображатьСетку		 = Ложь;
	Элементы.ТабличныйДокумент.ОтображатьЗаголовки	 = Ложь;
	
	ТабличныйДокумент.Очистить();
	
	ТЗ = Новый ТаблицаЗначений;
	
	ТЗ.Колонки.Добавить("Цена",			 Новый ОписаниеТипов("Число"));
	ТЗ.Колонки.Добавить("Сумма",		 Новый ОписаниеТипов("Число"));
	ТЗ.Колонки.Добавить("Количество",	 Новый ОписаниеТипов("Число"));
	ТЗ.Колонки.Добавить("Номенклатура",  Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	
	Для Каждого Стр Из Параметры.Данные.Данные Цикл
		
		НоваяСтрока = ТЗ.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр);
		
		НоваяСтрока.Номенклатура = Справочники.Номенклатура.НайтиПоНаименованию(Стр.Номенклатура, Истина);
		
	КонецЦикла;
	
	ВидДокумента = ПолучитьВидДокумента(Параметры.Данные.Вид);
	
	Если ВидДокумента = Неопределено Тогда
		
		Отказ = Истина;
		
		ВызватьИсключение "Не определен тип документа";
		
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ТЗ.Цена КАК Цена,
	|	ТЗ.Сумма КАК Сумма,
	|	ТЗ.Количество КАК Количество,
	|	ТЗ.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ Данные
	|ИЗ
	|	&ТЗ КАК ТЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Документ.Цена КАК Цена,
	|	Документ.Сумма КАК Сумма,
	|	Документ.Количество КАК Количество,
	|	Документ.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ Документ
	|ИЗ
	|	Документ." + ВидДокумента + ".Товары КАК Документ
	|ГДЕ
	|	Документ.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Документ.Цена КАК ЦенаБух,
	|	Документ.Сумма КАК СуммаБух,
	|	Документ.Количество КАК КоличествоБух,
	|	Документ.Номенклатура КАК НоменклатураБух,
	|	Данные.Цена КАК ЦенаТорг,
	|	Данные.Сумма КАК СуммаТорг,
	|	Данные.Количество КАК КоличествоТорг,
	|	Данные.Номенклатура КАК НоменклатураТорг
	|ИЗ
	|	Документ КАК Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Данные КАК Данные
	|		ПО Документ.Номенклатура = Данные.Номенклатура
	|			И Документ.Количество = Данные.Количество
	|			И Документ.Цена = Данные.Цена
	|			И Документ.Сумма = Данные.Сумма
	|
	|УПОРЯДОЧИТЬ ПО
	|	НоменклатураБух
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос = Новый Запрос(ЗаменитьТекстЗапроса(ТекстЗапроса, ВидДокумента));
	
	Запрос.УстановитьПараметр("ТЗ",		 ТЗ);
	Запрос.УстановитьПараметр("Ссылка",	 Параметры.Ссылка);
	
	Макет = ПолучитьМакет();
	
	ОбластьПечати = Макет.ПолучитьОбласть("Шапка");
	
	ОбластьПечати.Параметры.ТекущийДокумент		 = Параметры.Ссылка;
	ОбластьПечати.Параметры.УдаленныйДокумент	 = Параметры.Данные.Документ;
	
	ТабличныйДокумент.Вывести(ОбластьПечати);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ОбластьПечати = Макет.ПолучитьОбласть("Строка");
		
		ЗаполнитьЗначенияСвойств(ОбластьПечати.Параметры, Выборка);
		
		ОбластьПечати.ТекущаяОбласть.ЦветФона = ?(Выборка.СуммаБух <> Выборка.СуммаТорг, WebЦвета.Красный,  WebЦвета.СветлоЗеленый);
		
		ТабличныйДокумент.Вывести(ОбластьПечати);
		
	КонецЦикла;
	
КонецПроцедуры
//*******************************************************************************
